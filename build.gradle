plugins {
    id 'java'
    id "maven-publish"
    id "io.github.goooler.shadow" version "8.1.7"
}

group = 'com.starseekstudios'
version = System.getenv("VERSION") ?: '0.0.0-dev'

shadowJar {
    relocate 'de.exlll.configlib', 'com.starseekstudios.mystigui.configlib'
    relocate 'dev.jorel.commandapi', 'com.starseekstudios.mystigui.commandapi'
}

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "sonatype-oss-snapshots1"
        url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        name = "NBT-API"
        url = "https://repo.codemc.org/repository/maven-public/"
    }
    maven {
        name = "mysticore-repo"
        url = "https://maven.pkg.github.com/Mystievous/MystiCore"
        credentials {
            username = project.findProperty("gpr.username") ?: System.getenv("USERNAME")
            password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
        }
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.6-R0.1-SNAPSHOT"
    implementation "de.exlll:configlib-paper:4.5.0"
    implementation "dev.jorel:commandapi-bukkit-shade:9.5.3"
    compileOnly "dev.jorel:commandapi-annotations:9.5.3"
    annotationProcessor "dev.jorel:commandapi-annotations:9.5.3"
    compileOnly "com.starseekstudios:mysticore:1.4.0"
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Mystievous/MystiGui")
            credentials {
                username = project.findProperty("gpr.username") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            groupId 'com.starseekstudios'
            artifactId 'mystigui'
            from components.java
        }
    }
}